//file:noinspection GroovyAssignabilityCheck
plugins {
    id 'java-library'
    id 'maven-publish'
    alias libs.plugins.springframework.boot
    alias libs.plugins.spring.dependencymanagement
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    version = '0.7.1.0-SNAPSHOT'
    group = 'dev.temez.springlify'

    compileJava {
        options.encoding = 'UTF-8'
        sourceCompatibility = '17'
        targetCompatibility = '17'
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    dependencies {
        annotationProcessor libs.projectlombok.lombok

        compileOnlyApi libs.projectlombok.lombok
        compileOnlyApi libs.jetbrains.annotations

        testAnnotationProcessor libs.projectlombok.lombok

        testCompileOnly libs.projectlombok.lombok
        testCompileOnly libs.jetbrains.annotations

        testImplementation libs.spring.boot.starter.test
    }

    test {
        useJUnitPlatform()
    }

    if (!project.name.contains('example')) {
        publishing {
            publications {
                "$project.name"(MavenPublication) {
                    from components.java
                    artifactId = project.name
                    groupId = project.group
                    version = project.version
                    pom {
                        developers {
                            developer {
                                id = 'temez'
                                name = 'Artem I.'
                                email = 'temezf@gmail.com'
                            }
                        }
                    }
                }
            }
        }
    }
}

publishing {
    repositories {
        maven {
            name = 'StarMC'
            url = 'https://repo.star-mc.ru/public'
            credentials {
                username = System.getenv('STARMC_REPO_USER')
                password = System.getenv('STARMC_REPO_TOKEN')
            }
        }
    }
}
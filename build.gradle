//file:noinspection GroovyAssignabilityCheck
plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.0'
    id 'org.springframework.boot' version '3.1.1'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'checkstyle'
    id "io.freefair.lombok" version "8.4"
}

allprojects {

    apply plugin: 'checkstyle'
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.github.johnrengelman.shadow'

    version = '0.6.0.2dev'
    group = 'dev.temez.springlify'

    sourceCompatibility = '17'
    targetCompatibility = '17'

    [compileJava, javadoc]*.options*.encoding = 'UTF-8'

    java {
        withSourcesJar()
        withJavadocJar()
    }

    dependencies {
        annotationProcessor 'org.projectlombok:lombok:1.18.22'

        api 'org.jetbrains:annotations:24.0.1'
        api 'org.springframework.boot:spring-boot-starter'

        implementation "net.kyori:adventure-api:4.14.0"

        compileOnlyApi 'org.projectlombok:lombok:1.18.22'
    }

    if (!project.name.contains('plugin')) {
        publishing {
            publications {
                "$project.name"(MavenPublication) {
                    from components.java
                    artifactId = project.name
                    groupId = project.group
                    version = project.version
                    pom {
                        developers {
                            developer {
                                id = 'temez'
                                name = 'Artem I.'
                                email = 'temezf@gmail.com'
                            }
                        }
                    }
                }
            }
        }
    }
}

checkstyle {
    configFile file("$rootDir/config/checkstyle/checkstyle.xml")
}

publishing {
    repositories {
        maven {
            name = 'StarMC'
            url = 'https://repo.star-mc.ru/public'
            credentials {
                username = System.getenv('STARMC_REPO_USER')
                password = System.getenv('STARMC_REPO_PASSWORD')
            }
        }
    }
}




